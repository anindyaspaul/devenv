conf_line := """
for FILE in $HOME/.config/bashrc/*; do
    source $FILE
done
"""
bashrc := read(home_dir() / ".bashrc")
bashrc_cleaned := trim(replace(bashrc, conf_line, ""))
bashrc_configured := bashrc_cleaned + "\n\n" + conf_line

# List available recipes
help:
    @just --list --unsorted

# Install bashrc modifications
install:

# Configure bashrc
configure:
    @echo "Configuring bashrc."
    @echo {{ quote(bashrc_configured) }} > "$HOME/.bashrc"

    @echo "Stowing bashrc."
    mkdir -p "$HOME/.config/bashrc"
    stow .

# Reset bashrc modifications
reset:
    @echo "Cleaning up bashrc."
    @echo {{ quote(bashrc_cleaned) }} > "$HOME/.bashrc"

    stow --delete .

